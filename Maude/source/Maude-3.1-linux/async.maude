*** maude demo hvl april 2021

mod POPULATION is
    protecting NAT + STRING .
    protecting CONFIGURATION .
    sorts Status .

    op single : -> Status [ctor] .
    ops engaged married : Oid -> Status [ctor] .
    op age :_ : Nat -> Attribute [ctor].
    op status :_ : Status -> Attribute [ctor].
    op Person : -> Cid [ctor] .
    
    subsort String < Oid .
    
    vars N N' M : Nat .   vars X X' Y : String . var S : Status . var AS : AttributeSet .
    
    *** Communiation begins here:
    
    ops marry? yes no : Oid Oid -> Msg .
    op waitFor : Oid -> Status .
                         
    crl [propose] : < X : Person | age : N, status : single >
		     < Y : Person | age : M, status : single >
			=>
			< X : Person | age : N, status : waitFor(Y) >
			< Y : Person | age : M, status : single > marry?(Y, X) if N > 15 .
			
    crl [accept] : marry?(Y, X) < Y : Person | age : N, status : single >
			=>
		   < Y : Person | age : N, status : engaged(X) > yes(X, Y) if N > 15 .
    rl [reject] : marry?(Y, X) < Y : Person | age : N, status : S >
                  =>
                  < Y : Person | age : N, status : S > no(X, Y) .
                  
    rl [yes] : yes(X, Y) < X : Person | age : N, status : waitFor(Y) >
		=>
		< X : Person | age : N, status : engaged(Y) > .
    rl [no] : no(X, Y) < X : Person | age : N, status : waitFor(Y) >
	        => < X : Person | age : N, status : single > .

  op greeks : -> Configuration .
  eq greeks =   
    < "Gaia" : Person | age : 999, status : single >
    < "Uranus" : Person | age : 900, status : single >
    < "Kronos" : Person | age : 800, status : single >
    < "Rhea" : Person | age : 12, status : single > .

endm
